// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Screen.jack

/**
 * A library of functions for displaying graphics on the screen.
 * The Hack physical screen consists of 256 rows (indexed 0..255, top to bottom)
 * of 512 pixels each (indexed 0..511, left to right). The top left pixel on 
 * the screen is indexed (0,0).
 */
class Screen {
    static Boolean color;
    

    //Screen goes from register 16384 to 24575. Screen resolution is 256x512
    /** Initializes the Screen. */
    function void init() {
        let color = true;
        return;
    }

    /** Erases the entire screen. */
    function void clearScreen() {
        var int addr;
        let addr = 16384;
        while(addr < 24576){
            do Memory.poke(addr, 0);
            let addr = addr + 1;
        }
        return;
    }

    /** Sets the current color, to be used for all subsequent drawXXX commands.
     *  Black is represented by true, white by false. */
    function void setColor(boolean b) {
        let color = b;
        return;
    }

    /** Draws the (x,y) pixel, using the current color. */
    function void drawPixel(int x, int y) {
        var int addr, val, bit_num, i, bit;
        //Find register containing the pixel
        let addr = 16384 + (32*y) + (x/16);
        //Retrieve register
        if((addr < 24576) & (addr > 16383)){let val = Memory.peek(addr); }
        //Find bit in register to operate on
        // a % b = a - (b * int(a/b)) Modulus Math Expression
        let bit_num = x - (16*(x/16));
        //Using bit_num create a 16 bit version of this bit
        let i = 0;
        let bit = 1;
        while(i < bit_num){
            let bit = bit + bit;
            let i = i + 1;
        }
        //Based on color perform an operation
        if(color){  //Turn bit on
            let val = val | bit;
        }
        else{  // Turn bit off
            let val = ~val;  // 16 bit Not
            let val = val | bit; // 16 bit Or (In not space)
            let val = ~val;  // 16 bit Not
        }
        if((addr < 24576) & (addr > 16383)){do Memory.poke(addr, val); }
        return;
    }

    /** Draws a line from pixel (x1,y1) to pixel (x2,y2), using the current color. */
    function void drawLine(int x1, int y1, int x2, int y2) {
        var int dx, dy, dxAbs, dyAbs, a, b, diff;
        var boolean pathChosen;
        let dx = x2-x1;
        let dy = y2-y1;
        let dxAbs = dx;
        let dyAbs = dy;
        if(dxAbs < 0){let dxAbs = -dxAbs; }
        if(dyAbs < 0){let dyAbs = -dyAbs; }
        let a = 0;
        let b = 0;
        let diff = 0;
        //Vertical Line
        if(dx = 0){
            while((b<dyAbs) | (b=dyAbs)){
                if(dy > 0){do Screen.drawPixel(x1, y1+b); }  // Go South
                else{do Screen.drawPixel(x1, y1-b); }  // Go North
                let b = b + 1;
            }
            return;
        }
        //Horizontal Line
        if(dy = 0){
            while((a<dxAbs) | (a=dxAbs)){
                if(dx > 0){do Screen.drawPixel(x1+a, y1); }  // Go East
                else{do Screen.drawPixel(x1-a, y1); }  // Go West
                let a = a + 1;
            }
            return;
        }
        //Sloped Line
        if(~(dy = 0) & ~(dx = 0)){
            while(((a<dxAbs) | (a=dxAbs)) & ((b<dyAbs) | (b=dyAbs))){
                let pathChosen = false;
                if(dx > 0){  // Go BLANKEast
                    if (dy > 0){do Screen.drawPixel(x1+a, y1+b); }  // SouthEast
                    else{do Screen.drawPixel(x1+a, y1-b); }  // NorthEast
                }
                else{  // Go BLANKWest
                    if (dy > 0){do Screen.drawPixel(x1-a, y1+b); } // SouthWest
                    else{do Screen.drawPixel(x1-a, y1-b);}  // NorthWest
                }
                if((diff<0) & (dy>0) & ~pathChosen){
                    let pathChosen = true;
                    let a = a+1;
                    let diff = diff + dy;
                }
                if((diff<0) & (dy<0) & ~pathChosen){
                    let pathChosen = true;
                    let a = a+1;
                    let diff = diff - dy;  
                }

                if((diff>-1) & (dx>0) & ~pathChosen){
                    let pathChosen = true;
                    let b = b + 1;
                    let diff = diff - dx;
                }
                if((diff>-1) & (dx<0) & ~pathChosen){
                    let pathChosen = true;
                    let b = b + 1;
                    let diff = diff + dx;
                }
            }
            return;
        }

        return;
    }

    /** Draws a filled rectangle whose top left corner is (x1, y1)
     * and bottom right corner is (x2,y2), using the current color. */
    function void drawRectangle(int x1, int y1, int x2, int y2) {
        var int dx, dy, b;
        let dx = x2-x1;
        let dy = y2-y1;
        let b = 0;
        while((b<dy) | (b=dy)){
                if(dy > 0){do Screen.drawLine(x1, y1+b, x2, y1+b); }  // Draw lines from North to South
                else{do Screen.drawLine(x1, y1-b, x2, y1-b); }  // Draw lines from South to North
                let b = b + 1;
        }
        return;
    }

    /** Draws a filled circle of radius r<=181 around (x,y), using the current color. */
    function void drawCircle(int x, int y, int r) {
        return;
    }
}
