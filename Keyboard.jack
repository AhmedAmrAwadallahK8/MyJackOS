// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Keyboard.jack

/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {
    //
    /** Initializes the keyboard. */
    function void init() {
        return;
    } 

    /**
     * Returns the character of the currently pressed key on the keyboard;
     * if no key is currently pressed, returns 0.
     *
     * Recognizes all ASCII characters, as well as the following keys:
     * new line = 128 = String.newline()
     * backspace = 129 = String.backspace()
     * left arrow = 130
     * up arrow = 131
     * right arrow = 132
     * down arrow = 133
     * home = 134
     * End = 135
     * page up = 136
     * page down = 137
     * insert = 138
     * delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() { 
        return Memory.peek(24576);
    }

    /**								
     * Waits until a key is pressed on the keyboard and released,
     * then echoes the key to the screen, and returns the character 
     * of the pressed key.
     */
    function char readChar() {
        var char input;
        //Display cursor code not added yet
        //Wait until key pressed
        while(Keyboard.keyPressed() = 0){}
        //Record Key Press
        let input = Keyboard.keyPressed();
        //Wait until key released
        while(~(Keyboard.keyPressed() = 0)){}
        //Print the recorded input
        do Output.printChar(input);
        return input;
    }

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its value. Also handles user backspaces.
     */
    function String readLine(String message) { //Backspace is removing 2 values not sure why
        var String finalInput;
        var char singleInput;
        var Boolean userDone;
        do Output.printString(message);
        let finalInput = String.new(50);
        let userDone = false;
        while(~userDone){
            let singleInput = Keyboard.readChar();
            //NewLine entered
            if(singleInput = 128){do Output.println(); return finalInput; }
            //Different character entered
            else{
                //Backspace Entered
                if(singleInput = 129){do finalInput.eraseLastChar(); }
                //Different Character Entered
                else{let finalInput = finalInput.appendChar(singleInput); }
            }

        }
        return finalInput;

    }   

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its integer value (until the first non-digit character in the
     * entered text is detected). Also handles user backspaces. 
     */
    function int readInt(String message) {
        var String input;
        var int out;
        let input = Keyboard.readLine(message);
        let out = input.intValue();
        return out;
    }
}
